sourceSets {
    integrationTest {
        groovy.srcDir file('src/integrationTest/groovy')
        resources.srcDir file('src/integrationTest/resources')
        compileClasspath += sourceSets.main.output + configurations.testRuntime
        runtimeClasspath += output + compileClasspath
    }
}

task integrationTest(type: Test) {
    description = 'Runs the integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    mustRunAfter test
}

def imageId = 'timo1979/account-web-service:1.0.1'

check.dependsOn integrationTest

buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'com.bmuschko:gradle-docker-plugin:6.4.0'
    }
}

apply plugin: com.bmuschko.gradle.docker.DockerRemoteApiPlugin

import com.bmuschko.gradle.docker.tasks.image.DockerPullImage
import com.bmuschko.gradle.docker.tasks.container.DockerCreateContainer
import com.bmuschko.gradle.docker.tasks.container.DockerStartContainer
import com.bmuschko.gradle.docker.tasks.container.DockerStopContainer
import com.bmuschko.gradle.docker.tasks.container.extras.DockerWaitHealthyContainer

task pullImage(type: DockerPullImage) {
    group = 'Docker'
    image = imageId
}

task createContainer(type: DockerCreateContainer) {
    group = 'Docker'
    dependsOn pullImage
    targetImageId pullImage.image.get()
    hostConfig.portBindings = ['8080:8080']
    hostConfig.autoRemove = true
}

task startContainer(type: DockerStartContainer) {
    group = 'Docker'
    dependsOn createContainer
    targetContainerId  createContainer.getContainerId()
}

task startAndWaitOnHealthyContainer(type: DockerWaitHealthyContainer) {
    group = 'Docker'
    dependsOn startContainer
    awaitStatusTimeout = 60
    targetContainerId createContainer.getContainerId()
}

task stopContainer(type: DockerStopContainer) {
    group = 'Docker'
    targetContainerId createContainer.getContainerId()
}

integrationTest {
    dependsOn startAndWaitOnHealthyContainer
    finalizedBy stopContainer
}
